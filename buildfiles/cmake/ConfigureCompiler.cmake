# Check and configure compiler options for RPCS3

if(MSVC)
	add_compile_options(/Zc:throwingNew- /constexpr:steps16777216)
	add_compile_definitions(
		_CRT_SECURE_NO_DEPRECATE=1 _CRT_NON_CONFORMING_SWPRINTFS=1 _SCL_SECURE_NO_WARNINGS=1
		NOMINMAX _ENABLE_EXTENDED_ALIGNED_STORAGE=1 _HAS_EXCEPTIONS=0)

	#TODO: Some of these could be cleaned up
	add_compile_options(/wd4805) # Comparing boolean and int
	add_compile_options(/wd4804) # Using integer operators with booleans
	add_compile_options(/wd4200) # Zero-sized array in struct/union

	# MSVC 2017 uses iterator as base class internally, causing a lot of warning spam
	add_compile_definitions(_SILENCE_CXX17_ITERATOR_BASE_CLASS_DEPRECATION_WARNING=1)

	# Increase stack limit to 8 MB
	add_link_options(/STACK:8388608,1048576)
else()
	check_cxx_compiler_flag("-march=native" COMPILER_SUPPORTS_MARCH_NATIVE)
	check_cxx_compiler_flag("-msse -msse2 -mcx16" COMPILER_X86)
	if (APPLE)
		check_cxx_compiler_flag("-march=armv8.4-a" COMPILER_ARM)
	else()
		# Per-variant, set by android/build-variants.sh. Default is the standard/new target;
		# the legacy variant passes armv8-a so it still runs on Cortex-A53/A73-class cores.
		# (clang spells the 8.0 baseline "armv8-a" and rejects "armv8.0-a" outright.)
		#
		# armv8.0, NOT armv8.1: ARMv8.1 mandates LSE atomics and the mobile core line skipped that
		# revision entirely -- A53/A72/A73 are 8.0 and A55/A75 onwards are 8.2. An armv8.1 build
		# therefore faults on exactly the old devices a fallback exists for, while running only on
		# parts that already handle 8.2. It is 8.0 or it is pointless.
		if (NOT DEFINED ARMSX3_ARM_MARCH)
			set(ARMSX3_ARM_MARCH "armv8.2-a+dotprod+fp16")
		endif()
		check_cxx_compiler_flag("-march=${ARMSX3_ARM_MARCH}" COMPILER_ARM)
	endif()

	add_compile_options(-Wall)
	add_compile_options(-fno-exceptions)
	add_compile_options(-fstack-protector)

	if(USE_NATIVE_INSTRUCTIONS AND COMPILER_SUPPORTS_MARCH_NATIVE)
		add_compile_options(-march=native)
	elseif(COMPILER_ARM)
		# This section needs a review. Apple claims armv8.5-a on M-series but doesn't support SVE.
		# Note that compared to the rest of the 8.x family, 8.1 is very restrictive and we'll have to bump the requirement in future to get anything meaningful.
		if (APPLE)
			add_compile_options(-march=armv8.4-a)
		else()
			# Default armv8.2-a+dotprod+fp16. dotprod is only mandatory from 8.4 and FP16
			# arithmetic is optional in 8.2, so "armv8.2-a" alone guarantees neither and both
			# are named. LSE atomics come from the 8.1 baseline and are already implied.
			#
			# This is a hard floor, not a preference: the instructions can appear anywhere in
			# the C++, and a device without them faults rather than falling back. Cortex-A55/A75
			# and later, i.e. Snapdragon 845 (2018) onward. The legacy variant overrides this
			# with armv8.1-a to stay below that line.
			add_compile_options(-march=${ARMSX3_ARM_MARCH})
		endif()
	elseif(COMPILER_X86)
		# Some compilers will set both X86 and ARM, so check explicitly for ARM first
		add_compile_options(-msse -msse2 -mcx16)
	endif()

	add_compile_options(-Werror=old-style-cast)
	if(ANDROID)
		# Vendored third-party headers are NOT system headers in the Android
		# build (they ride plain -I, not -isystem), so macros they ship with
		# C-style casts -- libcurl's CURLAUTH_BASIC = ((unsigned long)1) << 0
		# is the offender -- trip -Werror=old-style-cast inside a TU that merely
		# uses them, and the cast comes from the header, not our code. Keep the
		# lint but demote it to a warning on Android so one external macro cannot
		# fail the whole core build.
		add_compile_options(-Wno-error=old-style-cast)
	endif()
	add_compile_options(-Werror=sign-compare)
	add_compile_options(-Werror=reorder)
	add_compile_options(-Werror=return-type)
	add_compile_options(-Werror=overloaded-virtual)
	add_compile_options(-Werror=missing-noreturn)
	add_compile_options(-Werror=implicit-fallthrough)
	add_compile_options(-Wunused-parameter)
	add_compile_options(-Wignored-qualifiers)
	add_compile_options(-Wredundant-move)
	add_compile_options(-Wcast-qual)
	add_compile_options(-Wdeprecated-copy)
	add_compile_options(-Wtautological-compare)
	#add_compile_options(-Wshadow)
	#add_compile_options(-Wconversion)
	#add_compile_options(-Wpadded)
	add_compile_options(-Wempty-body)
	add_compile_options(-Wredundant-decls)
	#add_compile_options(-Weffc++)

	add_compile_options(-Wstrict-aliasing=1)
	#add_compile_options(-Wnull-dereference)

	if(CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
		add_compile_options(-Werror=inconsistent-missing-override)
		add_compile_options(-Werror=delete-non-virtual-dtor)
	elseif(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
		add_compile_options(-Werror=suggest-override)
		add_compile_options(-Wclobbered)
		# add_compile_options(-Wcast-function-type)
		add_compile_options(-Wduplicated-branches)
		add_compile_options(-Wduplicated-cond)
	endif()

	#TODO Clean the code so these are removed
	if(CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
		add_compile_options(-fconstexpr-steps=16777216)
		add_compile_options(-Wno-unused-lambda-capture)
		add_compile_options(-Wno-unused-private-field)
		add_compile_options(-Wno-unused-command-line-argument)
		add_compile_options(-Wno-elaborated-enum-base)
	elseif(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
		add_compile_options(-Wno-class-memaccess)
	endif()

	# Note that this refers to binary size optimization during linking, it differs from optimization compiler level
	add_link_options(-Wl,-O2)

	if(NOT APPLE AND NOT WIN32)
		# This hides our LLVM from mesa's LLVM, otherwise we get some unresolvable conflicts.
		add_link_options(-Wl,--exclude-libs,ALL)
	elseif(WIN32)
		# Increase stack limit to 8 MB
		add_link_options(-Wl,--stack -Wl,8388608)
	endif()

	# Specify C++ library to use as standard C++ when using clang (not required on linux due to GNU)
	if (CMAKE_CXX_COMPILER_ID STREQUAL "Clang" AND (APPLE OR WIN32))
		add_compile_options(-stdlib=libc++)
	endif()
endif()
